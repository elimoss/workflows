import os

asm_name = os.path.splitext(os.path.basename(config['assembly']))[0]
ref_name = os.path.splitext(os.path.basename(config['reference']))[0]
output = expand("{r}__{a}",r = ref_name, a = asm_name)
if 'asm_title' in config:
	asm_title = config['asm_title']
else: asm_title=''
if 'ref_title' in config:
	ref_title = config['ref_title']
else: ref_title=''

rule all:
	input:
		config['reference'],
		config['assembly'],
		expand("dotplot/{o}.pdf", o = output)

rule align:
	conda: "envs/mgwf.yaml"
	input:
		config['assembly'],
		config['reference']
	output: "aln/{id}.delta"
	shell: "nucmer {input} -p aln/{wildcards.id}"

rule filter:
	conda: "envs/mgwf.yaml"
	input: rules.align.output
	output: "aln/{id}.filt"
	shell: "delta-filter -1 {input} > {output}"

rule postprocess:
	conda: "envs/mgwf.yaml"
	input: rules.filter.output
	output: "aln/{id}.tsv"
	shell: "show-coords -T {input} > {output}"

rule dotplot:
	conda: "envs/mgwf.yaml"
	input: rules.postprocess.output
	params: 
		min_ctg_length=5000,
		asm_title=asm_title,
		ref_title=ref_title 
	output:	"dotplot/{id}.pdf"
	script: "scripts/dotplot.R"
